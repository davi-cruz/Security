// All Tables
union withsource = TableName *
| where Timestamp >= ago(30d)
| summarize total = count(), DailyBytes = sum(estimate_data_size(*)) by TableName, bin(Timestamp, 1d)
| summarize DailyBytesAvg = avg(DailyBytes), TotalBytes = sum(DailyBytes) by TableName
| extend TableSizeMB = format_bytes(TotalBytes, 2, "MB"),
    DailyAvgMB = format_bytes(DailyBytesAvg, 2, "MB"),
    TableSizeGB = format_bytes(TotalBytes, 2, "GB"),
    DailyAvgGB = format_bytes(DailyBytesAvg, 2, "GB")
| sort by TotalBytes desc
| project-away TotalBytes, DailyBytesAvg

// Device count by Platform
DeviceInfo
| where Timestamp >= ago(30d) and OnboardingStatus == "Onboarded"
| summarize arg_max(Timestamp, *) by DeviceId
| extend Platform = case(isempty(OSPlatform) or OSPlatform contains "Windows", "Windows", OSPlatform)
| summarize count() by Platform